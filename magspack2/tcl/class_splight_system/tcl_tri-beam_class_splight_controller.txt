@name TCL Tri-Beam Class Splight Controller
@inputs Pod:entity NextWeapon PrevWeapon ThirdPerson
@outputs RGB:vector SwitchPos
@persist E:entity Driver:entity Position Unlock SwitchSound:string
@persist Name:string
@trigger NextWeapon PrevWeapon
@model models/magtrainslocos/rotaryswitches/headlight_emd.mdl

#V2.0 - switch now cyclic in both directions, changed model

#-------------------------------------------------------------------------------#
# W E L C O M E   A B O A R D   T H E   T R A N S - C A N A D A   L I M I T E D #
#                                                                               #
# Hello and welcome to TCL's Advanced Class Splight Controller! This type of    #
# rotary switch can be found in pretty much any mid- to late-era diesel that    #
# has tri-beam (white/red/green) class lights.                                  #
#                                                                               #
# This controller E2 is designed to work in tandem with the TCL Advanced        #
# Tri-Beam Class Splight system.                                                #
#                                                                               #
# Setup:                                                                        #
#   1)  Spawn and position this E2 somewhere accessible from your engineer's    #
#       seat. ** It must be visible from the seat to be used. **                #
#   2)  Spawn, position and parent your Class Splight E2s - see their code for  #
#       any special instructions.                                               #
#   3)  Wire all inputs on this E2 to their respective outputs on your Pod.     #
#   4)  Wire the SwitchPos output to your Class Splight E2s.                    #
#   5)  Cycle through the positions by aiming at the E2 and rolling your mouse  #
#       wheel up and down.                                                      #
#   6)  Make sure you parent everything, and enjoy!                             #
#                                                                               #
#   Tip- Not sure where to place your class light switches? Older Canadian      #
#   locomotives usually have a small extension built onto the left of the       #
#   control stand, directly adjacent to the brake controls, where the light     #
#   controls can be found. More modern locomotives with desktop stands have     #
#   them positioned overhead airliner style, or on the front face of the        #
#   console underneath the Engine Run/Gen Field/Fuel Pump triple switches.      #
#                                                                               #
# SUPER thanks to Monkatraz for help with development and optimization!         #
#                                                                               #
# Happy railroading!                                                     TCL <3 #
#                                                                               #
#         Having problems? Find me on Discord!  TransCanadaLimited#8427         #
#-------------------------------------------------------------------------------#

if(first() | dupefinished()){ 
    
SwitchSound = "gsgtrainsounds/misc/switch_rotating.wav"

Name = "Class Lights" #"Front Class", "Rear Right Class", etc

####

E=entity()
E:setBodygroup(2,1)

#Holo Base

holoCreate(1,E:toWorld(vec(0,0,0.1)),vec(0.3),E:toWorld(ang(0,0,0)),vec(255),"plane")
holoParent(1,E)
holoMaterial(1,"models/proppertextures/plastic_black")

holoCreate(2,E:toWorld(vec(-0.8,-0.8,0.2)),vec(0.125),E:toWorld(ang(0,0,0)),vec(255),"plane")
holoParent(2,E)
holoColor(2,vec(100,100,200))
holoMaterial(2,"models/proppertextures/acrylic")

holoCreate(3,E:toWorld(vec(-0.8,0.8,0.2)),vec(0.125),E:toWorld(ang(0,0,0)),vec(255),"plane")
holoParent(3,E)
holoColor(3,vec(200))
holoMaterial(3,"models/proppertextures/acrylic")

holoCreate(4,E:toWorld(vec(0.8,0.8,0.2)),vec(0.125),E:toWorld(ang(0,0,0)),vec(255),"plane")
holoParent(4,E)
holoColor(4,vec(200,0,0))
holoMaterial(4,"models/proppertextures/acrylic")

holoCreate(5,E:toWorld(vec(0.8,-0.8,0.2)),vec(0.125),E:toWorld(ang(0,0,0)),vec(255),"plane")
holoParent(5,E)
holoColor(5,vec(100,200,100))
holoMaterial(5,"models/proppertextures/acrylic")

holoCreate(6,E:toWorld(vec(0)),vec(0.3),E:toWorld(ang(180,0,0)),vec(255),"plane")
holoParent(6,E)
holoMaterial(6,"models/proppertextures/grid_b")

}

#Main Code

Driver = Pod:driver()
if(Driver){
Unlock = Driver:aimPos():distance(E:pos()) < 2
}

if(Unlock & !ThirdPerson){
    if(Unlock & !ThirdPerson){
        if(PrevWeapon){ #Switch Pos Up
            soundPlay(0,1,SwitchSound)
            if(Position<3){
                Position++
            }elseif(Position==3){
                Position=0
            }
        }elseif(NextWeapon){ #Switch Pos Down
            soundPlay(0,1,SwitchSound)
            if(Position>0){
                Position--
            }elseif(Position==0){
                Position=3
            }
        }
    }
}

#All lights off
if(Position==0){
    E:setBodygroup(1,7)
    RGB = vec(0)
    SwitchPos = 0
    setName(Name+"\n(Off)")
}

#Colour 1 (White)
if(Position==1){
    E:setBodygroup(1,1)
    RGB = vec(200)
    SwitchPos = 1
    setName(Name+"\n(White)")
}

#Colour 2 (Red)
if(Position==2){
    E:setBodygroup(1,3)
    RGB = vec(200,0,0)
    SwitchPos = 2
    setName(Name+"\n(Red)")
}

#Colour 3 (Green)
if(Position==3){
    E:setBodygroup(1,5)
    RGB = vec(100,200,100)
    SwitchPos = 3
    setName(Name+"\n(Green)")
}
